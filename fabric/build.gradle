plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }

    // Combat Roll
    modCompileOnly "maven.modrinth:combat-roll:${combat_roll_version}-fabric"
    modCompileOnly "maven.modrinth:player-animation-library:${player_animation_lib_version_fabric}"

    // EMF
    modCompileOnly "maven.modrinth:entity-model-features:${emf_version}-fabric-1.21.6"

    // Cloth Config
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"
    modImplementation "com.terraformersmc:modmenu:${mod_menu_version}"

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

publishMods {
    file = tasks.named("remapJar").flatMap { it.archiveFile }
    changelog = rootProject.file("CHANGELOG.md").text
    type = STABLE
    displayName = "${mod_name} Fabric ${minecraft_version} - ${project.version}"
    modLoaders.add("fabric")

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "1429618"
        minecraftVersions.add("${minecraft_version}")
        clientRequired = true
        serverRequired = false

        optional { slug = "cloth-config" }
        optional { slug = "combat-roll"}
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "W7xRcWxh"
        minecraftVersions.add("${minecraft_version}")

        optional { slug = "cloth-config" }
        optional { slug = "combat-roll"}
    }
}