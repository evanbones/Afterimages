plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")

    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at)
    }

    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }

    runs {
        client {
            client()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
            sourceSet project(":common").sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.2-1'
    compileOnly "io.github.llamalad7:mixinextras-common:${mixinextras_version}"
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:${mixinextras_version}"))
    annotationProcessor "io.github.llamalad7:mixinextras-common:${mixinextras_version}"

    // Combat Roll
    modCompileOnly "maven.modrinth:combat-roll:${combat_roll_version}-forge"
    modCompileOnly "maven.modrinth:playeranimator:${player_animator_version}-forge"

    // Elenai Dodge
    modCompileOnly "maven.modrinth:elenai-dodge-2:${elenai_dodge_version}"
    modCompileOnly "maven.modrinth:feathers:${feathers_version}"

    // EMF
    modCompileOnly "maven.modrinth:entity-model-features:${emf_version}-forge-1.20.1"

    // EMI
    modRuntimeOnly "dev.emi:emi-forge:${emi_version}"

    // AzureLib Armor
    modCompileOnly "maven.modrinth:azurelib-armor:${azure_lib_armor_forge_version}"

    // Cloth Config
    modImplementation "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"

    compileOnly project(":common")
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")
}

jar {
    finalizedBy('reobfJar')
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    ])
}

publishMods {
    file = tasks.named("reobfJar").flatMap { it.archiveFile }
    changelog = rootProject.file("CHANGELOG.md").text
    type = STABLE
    displayName = "${mod_name} Forge ${minecraft_version} - ${project.version}"
    modLoaders.add("forge")

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "1429618"
        minecraftVersions.add("1.20.1")

        optional { slug = "cloth-config" }
        optional { slug = "combat-roll"}
        optional { slug = "elenai-dodge-2"}
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "W7xRcWxh"
        minecraftVersions.add("1.20.1")

        optional { slug = "cloth-config" }
        optional { slug = "combat-roll"}
        optional { slug = "elenai-dodge-2"}
    }
}
